apply from: "${rootProject.rootDir}/common.gradle"


android {

    //签名配置
    signingConfigs {
        release {
            keyAlias KEY_ALIAS  //签名文件别名
            keyPassword KEY_PASSWORD //别名密码
            storeFile rootProject.file(KEYSTORE_FILE)  //签名文件地址
            storePassword KEYSTORE_PASSWORD //签名文件密码
        }
    }

    //签名配置
    buildTypes {
        //调试版本，无混淆
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "@string/app_name_debug"
        }
        //发布版本，有混淆
        release {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            applicationIdSuffix ".release"
            resValue "string", "app_name", "@string/app_name_release"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res/layout/main',
                    'src/main/res/layout/splash',
                    'src/main/res/layout',
                    'src/main/res'
            ]
        }
    }

    //配置打包输出文件路径+文件名规则
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def appName
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 遍历所有的产品风味
                productFlavors.each { flavor ->
                    //获取不同环境配置的app_name参数
                    appName = flavor.manifestPlaceholders.app_name
                    //项目名+环境类型.apk
                    outputFileName = "${appName}.apk"
                }
            }
        }
    }
}

dependencies {

    if (!project.isModuleDebug) {
        //单独需要的依赖
        implementation project(path: ':business:lib_login')
        implementation project(path: ':business:lib_home')
        implementation project(path: ':business:lib_me')
        implementation project(path: ':business:lib_public')
    }
    implementation project(path: ':base:framework')

    //阿里热修复
    implementation 'com.aliyun.ams:alicloud-android-hotfix:3.3.5'
}

